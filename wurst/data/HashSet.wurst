package HashSet
import public HashList

/** Generic set implementation */
public class HashSet<T> extends HashList<T>
	private static constant position = InitHashtable()

	/** Adds an element to this set */
	override function add(vararg T elems)
		for elem in elems
			if count(elem) <= 0
				position.saveInt(this castTo int, elem castTo int, size())
				super.add(elem)

	/** Removes an element from this set if present */
	override function remove(T elem)
		if count(elem) > 0
			let pos = position.loadInt(this castTo int, elem castTo int)
			removeAt(pos)

	/** Remove the element at the given index */
	override function removeAt(int index)
		position.removeInt(this castTo int, get(index) castTo int)
		super.removeAt(index)

	override function clear()
		super.clear()
		position.flushChild(this castTo int)

	ondestroy
		position.flushChild(this castTo int)

